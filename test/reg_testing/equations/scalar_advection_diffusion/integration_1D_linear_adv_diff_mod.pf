!--------------------------------------------------------------------------------
!
!           Test a 1D linear advection-diffusion problem with a source term.
!
!   div( vec(c)*u - mu(u)*grad(u) ) = 0
!
!   Fluxes:
!       Advective Flux: F_a(u) = vec(c)*u
!       Diffusive Flux: F_d(u) = -mu*dudx
!
!
!   Boundary conditions are:
!       Dirichlet U(x=0) = 1
!       Dirichlet U(x=1) = 0
!
!
!   Analytical Solution is:
!       numerator   = 1 - exp( (x-1) * cx/mu )
!       denominator = 1 - exp(-cx/mu)
!
!       U(x) = numerator/denominator    for x in [0,1]
!
!
!   Scalar coefficient models are:
!       vec(c) = [1.0, 0.0, 0.0]
!        mu    = 0.05
!
!
!   Tests that are being run here:
!       - Run the calculation on multiple grids with order of accuracy 2-5
!       - Test for convergence in 1 Newton iteration (linear problem)
!       - Test convergence rates for spatial orders of accuracy
!
!   @author Nathan A. Wukie
!   @date   10/23/2016
!
!--------------------------------------------------------------------------------
module integration_1D_linear_adv_diff_mod
#include <messenger.h>
    use pfunit_mod
    use mod_kinds,              only: rk, ik
    use mod_constants,          only: ZERO, THREE, IO_DESTINATION, ONE, TWO, PI, FOUR, &
                                      XI_MIN, XI_MAX, ETA_MIN, ETA_MAX, ZETA_MIN, ZETA_MAX
    use mod_string,             only: string_t
    use type_chidg,             only: chidg_t
    use type_chidg_data,        only: chidg_data_t
    use mod_bc,                 only: create_bc
    use type_bc_state,          only: bc_state_t
    use type_bc_state_group,          only: bc_state_group_t
    use type_function,          only: function_t
    use type_ivector,           only: ivector_t
    use mod_function,           only: create_function
    use mod_interpolate,        only: interpolate_element_standard
    use mod_test_utilities,     only: create_mesh_file
    use mod_file_utilities,     only: delete_file

    use mod_chidg_mpi,          only: IRANK, NRANK, ChiDG_COMM
    use mpi_f08,                only: MPI_COMM, MPI_REAL8, MPI_SUM
    use mod_io

    use type_chidg_worker,      only: chidg_worker_t
    use type_properties,        only: properties_t
    use DNAD_D

    ! Imports for defining a new operator
    use mod_operators,          only: operator_factory
    use type_operator,          only: operator_t
    use mod_models,             only: model_factory
    use type_model,             only: model_t

    ! Imports for defining a new equation builder
    use mod_equations,                  only: equation_set_factory
    use type_equation_builder,          only: equation_builder_t
    use type_equation_set,              only: equation_set_t
    implicit none


    ! Store the nonlinear convergence for each test case. This is used 
    ! to test the nonlinear convergence between a case running on
    ! a different number of procs.
    !
    ! Procs: 1, 2, 3
    !
    type(ivector_t) :: nsteps_convergence(3,2,3,2)            ! nprocs, ngrids, norders, (native,petsc)
    type(ivector_t) :: nsteps_convergence_overset(3,2,3,2)    ! nprocs, ngrids, norders, (native,petsc)
    real(rk)        :: error(3,2,3,2)                         ! nprocs, ngrids, norders, (native,petsc)
    real(rk)        :: error_overset(3,2,3,2)                 ! nprocs, ngrids, norders, (native,petsc)

    public  :: linear_advection_diffusion_1D



    real(rk), parameter :: cx = ONE
    real(rk), parameter :: cy = ZERO
    real(rk), parameter :: cz = ZERO
    real(rk), parameter :: mu = 0.05_rk




    !>  The advection-diffusion coefficient model.
    !!
    !!  @author Nathan A. Wukie
    !!  @date   12/3/2016
    !!
    !!
    !---------------------------------------------------------------------------------------
    type, extends(model_t), public :: testing_1D_linear_adv_diff_model_t

    contains

        procedure   :: init    => init_model
        procedure   :: compute => compute_model

    end type testing_1D_linear_adv_diff_model_t
    !***************************************************************************************






    !>  A new equation set builder to build a linear diffusion equation set with the 
    !!  new custom volume source.
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   8/19/2016
    !!
    !-----------------------------------------------------------------------------------------
    type, extends(equation_builder_t), public :: test_1D_linear_advection_diffusion_builder_t


    contains

        procedure   :: init => init_builder
        procedure   :: build

    end type test_1D_linear_advection_diffusion_builder_t
    !*****************************************************************************************







    !>  This parameter initialize the state for each test
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !----------------------------------------------------------------------------------------
    @TestParameter
    type, extends(MpiTestParameter) :: case_grid_order

        integer(ik)                 :: nelem_xi
        integer(ik)                 :: order
        character(:),   allocatable :: gridfile
        character(:),   allocatable :: gridtype
        character(:),   allocatable :: backend

    contains

        procedure   :: toString

    end type case_grid_order
    !****************************************************************************************







    !>  Main testing fixture with test components that get initialized for each test
    !!
    !!  @author Nathan A. Wukie 
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !----------------------------------------------------------------------------------------
    @TestCase(testParameters={getParameters()}, constructor=newTest)
    type, extends(MpiTestCase) :: linear_advection_diffusion_1D

        integer(ik)                 :: nelem_xi
        integer(ik)                 :: order
        character(:),   allocatable :: gridfile
        character(:),   allocatable :: gridtype
        character(:),   allocatable :: backend

        type(mpi_comm)  :: ChiDG_COMM

    contains
        procedure   :: setUp
        procedure   :: tearDown
    end type linear_advection_diffusion_1D
    !****************************************************************************************


    type(chidg_t)   :: chidg


contains




    !----------------------------------------------------------------------------------------
    !                       Scalar Diffusion Coefficient Model Methods
    !----------------------------------------------------------------------------------------

    !>  Initialize scalar diffusion coefficient model with name and model field being 
    !!  provided.
    !!
    !!  @author Nathan A. Wukie
    !!  @date   12/3/2016
    !!
    !----------------------------------------------------------------------------------------
    subroutine init_model(self)
        class(testing_1D_linear_adv_diff_model_t),    intent(inout)   :: self

        call self%set_name('Testing 1D Linear Advection Diffusion :: Coefficient Model')
        call self%set_dependency('f(Q-)')

        call self%add_model_field('Scalar Advection Velocity-1')
        call self%add_model_field('Scalar Advection Velocity-2')
        call self%add_model_field('Scalar Advection Velocity-3')
        call self%add_model_field('Scalar Diffusion Coefficient')

    end subroutine init_model
    !****************************************************************************************



    !>  Implement the function for providing the scalar diffusion coefficient field.
    !!
    !!  @author Nathan A. Wukie
    !!  @date   12/3/2016
    !!
    !----------------------------------------------------------------------------------------
    subroutine compute_model(self,worker)
        class(testing_1D_linear_adv_diff_model_t),  intent(in)      :: self
        type(chidg_worker_t),                       intent(inout)   :: worker

        type(AD_D), allocatable, dimension(:)   :: u, cx_model,cy_model,cz_model,mu_model


        u = worker%get_field('u', 'value')

        ! Initialize derivative arrays
        cx_model = u
        cy_model = u
        cz_model = u
        mu_model = u

        ! mu(u) = 1.0
        cx_model = cx
        cy_model = cy
        cz_model = cz
        mu_model = mu

        call worker%store_model_field('Scalar Advection Velocity-1',  'value', cx_model)
        call worker%store_model_field('Scalar Advection Velocity-2',  'value', cy_model)
        call worker%store_model_field('Scalar Advection Velocity-3',  'value', cz_model)
        call worker%store_model_field('Scalar Diffusion Coefficient', 'value', mu_model)

    end subroutine compute_model
    !****************************************************************************************





    
    !-------------------------------------------------------------------------------
    !                           Builder Methods for new equation set
    !-------------------------------------------------------------------------------


    !>  Initialize the new linear diffusion builder.
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   8/30/2016
    !!
    !-----------------------------------------------------------------------------------------
    subroutine init_builder(self)
        class(test_1D_linear_advection_diffusion_builder_t),   intent(inout)  :: self

        call self%set_name('Test 1D Linear Advection Diffusion')

    end subroutine init_builder
    !*****************************************************************************************

    



    !>  Implement the linear diffusion builder definition.
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   9/19/2016
    !!
    !-----------------------------------------------------------------------------------------
    function build(self,blueprint) result(linear_advection_diffusion_eqn)
        class(test_1D_linear_advection_diffusion_builder_t),    intent(in)  :: self
        character(*),                                           intent(in)  :: blueprint

        type(equation_set_t)    :: linear_advection_diffusion_eqn
        
        ! Set equationset name
        call linear_advection_diffusion_eqn%set_name("Test 1D Linear Advection Diffusion")

        ! Add spatial operators
        call linear_advection_diffusion_eqn%add_operator("Scalar Advection Boundary Average Operator")
        call linear_advection_diffusion_eqn%add_operator("Scalar Advection LaxFriedrichs Operator")
        call linear_advection_diffusion_eqn%add_operator("Scalar Advection Volume Operator")
        call linear_advection_diffusion_eqn%add_operator("Scalar Advection BC Operator")
        call linear_advection_diffusion_eqn%add_operator("Scalar Diffusion Boundary Average Operator")
        call linear_advection_diffusion_eqn%add_operator("Scalar Diffusion Volume Operator")
        call linear_advection_diffusion_eqn%add_operator("Scalar Diffusion BC Operator")

        ! Add model for 'compute_mu' scalar coefficient
        call linear_advection_diffusion_eqn%add_model('Testing 1D Linear Advection Diffusion :: Coefficient Model')

    end function build
    !*****************************************************************************************



    !-------------------------------------------------------------------------------
    !                               Test methods
    !-------------------------------------------------------------------------------



    !> Parameter constructors
    !!
    !------------------------------------------------------------------------------
    function newTest(test_parameter) result(test)
        type(case_grid_order),  intent(in)          :: test_parameter
        type(linear_advection_diffusion_1D)    :: test

        test%nelem_xi = test_parameter%nelem_xi
        test%order    = test_parameter%order
        test%gridfile = test_parameter%gridfile
        test%gridtype = test_parameter%gridtype
        test%backend  = test_parameter%backend

    end function newTest
    !******************************************************************************


    !> Parameter initialization
    !!
    !------------------------------------------------------------------------------
    function getParameters() result(params)
        type(case_grid_order), allocatable :: params(:)
    
        integer(ik),        allocatable :: nelem_xi(:)
        integer(ik),        allocatable :: orders(:)
        type(string_t),     allocatable :: files(:)
        type(string_t),     allocatable :: gridtype(:)
        type(string_t),     allocatable :: backend(:)
        integer(ik) :: icase, ncases, imesh, igrid, iorder, ibackend

        nelem_xi = [60, 120]
        orders   = [2, 3, 4]
        gridtype = [string_t("single"), string_t("overset")]
        backend  = [string_t("native"), string_t("petsc")  ]

        !Initialize the test case container with the parameter
        ncases = size(nelem_xi)*size(gridtype)*size(orders)*size(backend)
        if (allocated(params)) deallocate(params)
        allocate(params(ncases))

        icase = 1
        do imesh = 1,size(nelem_xi)
            do igrid = 1,size(gridtype)
                do iorder = 1,size(orders)
                    do ibackend = 1,size(backend)

                        params(icase)%nelem_xi = nelem_xi(imesh)
                        params(icase)%order    = orders(iorder)
                        params(icase)%gridtype = gridtype(igrid)%str
                        params(icase)%gridfile = 'grid.h5'
                        params(icase)%backend  = backend(ibackend)%str
                        icase = icase + 1

                    end do
                end do
            end do
        end do

    end function getParameters
    !*****************************************************************************************



    !>  Reporting the parameters of the test
    !!
    !----------------------------------------------------------------------------------------
    function toString(this) result(string)
        class (case_grid_order),    intent(in)  :: this
        character(:), allocatable   :: string

        character(len=200) :: buffer

        write(buffer,'( "(nelem, gridtype, order, backend)=","(", I3,",", A6,",", I1,",", A5, ")" )') this%nelem_xi, this%gridtype, this%order, this%backend
        string = trim(buffer)

    end function toString
    !****************************************************************************************




    !>  Set up test routine.
    !!
    !!  This routine reads the grid file, sets up boundary conditions, initializes storage
    !!  and sets the appropriate configuration options.
    !!  This routine also gets the numer of processors and the correct MPI communicator for 
    !!  the test.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !----------------------------------------------------------------------------------------
    subroutine setUp(this)
        class(linear_advection_diffusion_1D),  intent(inout)   :: this

        class(bc_state_t),  allocatable :: bc_state
        type(bc_state_group_t)          :: bc_state_groups(3)
        type(string_t)                  :: group_names(2,6)
        class(function_t),  allocatable :: fcn

        
        type(test_1D_linear_advection_diffusion_builder_t)  :: builder_test_linear_advection_diffusion
        type(testing_1D_linear_adv_diff_model_t)            :: diffusion_coefficient_model

        ! Get MPI communicator information from pFUnit
        IRANK              = this%getProcessRank()
        NRANK              = this%getNumProcessesRequested()
        ChiDG_COMM%mpi_val = this%getMpiCommunicator()


        ! Initialize ChiDG environment
        call chidg%start_up('mpi',ChiDG_COMM)
        call chidg%start_up('core')


        !-------------------------------------------------------------------------------
        !               Initialize and register the diffusion coefficient model
        !-------------------------------------------------------------------------------
        call diffusion_coefficient_model%init()
        call model_factory%register(diffusion_coefficient_model)


        !-------------------------------------------------------------------------------
        !                 Initialize and register equation set builder
        !-------------------------------------------------------------------------------
        call builder_test_linear_advection_diffusion%init()
        call equation_set_factory%register(builder_test_linear_advection_diffusion%build('default'))


        !------------------------------------------------------------------------------
        !
        ! Define input data here that is normally read through the namelist input file
        !
        ! As a result, we don't call chidg%init('io')
        !
        !------------------------------------------------------------------------------
        basis            = 'legendre'
        gq_rule          = 1
        backend          = trim(this%backend)
        time_integrator  = 'steady'
        nonlinear_solver = 'newton'
        linear_solver    = 'fgmres'
        preconditioner   = 'RASILU0'
        IO_DESTINATION   = 'file'

        ! Set linear solver options to pass during initialization
        call loptions%set('tol',1.e-14_rk)
        call loptions%set('rtol',1.e-15_rk)
        call loptions%set('inner_fgmres',.true.)

        ! Set nonlinear solver options
        call noptions%set('tol',5.e-10_rk)
        call noptions%set('rtol',1.e-15_rk)
        call noptions%set('cfl0',1.0_rk)
        call noptions%set('nsteps',100)
        call noptions%set('ptc',.false.)
        call noptions%set('search','Backtrack')
        !------------------------------------------------------------------------------


        ! Create mesh
        if (IRANK == GLOBAL_MASTER) then

            ! Set boundary condition function groups
            bc_state_groups(1)%name = "Left Boundary"
            bc_state_groups(2)%name = "Right Boundary"
            bc_state_groups(3)%name = "YZ Boundaries"

            call create_bc("Scalar Value", bc_state)
            call bc_state%set_fcn_option("Value","val",ONE)
            call bc_state_groups(1)%add_bc_state(bc_state)


            call create_bc("Scalar Value", bc_state)
            call bc_state%set_fcn_option("Value","val",ZERO)
            call bc_state_groups(2)%add_bc_state(bc_state)


            call create_bc("Scalar Extrapolate", bc_state)
            call bc_state_groups(3)%add_bc_state(bc_state)

            
            ! Generate grid
            if (this%gridtype == 'overset') then


                group_names(1,:) = [string_t("Left Boundary") , &
                                    string_t("Empty")         , &
                                    string_t("YZ Boundaries") , &
                                    string_t("YZ Boundaries") , &
                                    string_t("YZ Boundaries") , &
                                    string_t("YZ Boundaries")]

                group_names(2,:) = [string_t("Empty")         , &
                                    string_t("Right Boundary"), &
                                    string_t("YZ Boundaries") , &
                                    string_t("YZ Boundaries") , &
                                    string_t("YZ Boundaries") , &
                                    string_t("YZ Boundaries")]

                call create_mesh_file('D2 NxNxN M1',this%gridfile,equation_sets   = [string_t("Test 1D Linear Advection Diffusion"),    &
                                                                                     string_t("Test 1D Linear Advection Diffusion")],   &
                                                                  group_names     = group_names,                                        &
                                                                  bc_state_groups = bc_state_groups,                                    &
                                                                  nelem_xi        = this%nelem_xi,                                      &
                                                                  nelem_eta       = 1,                                                  &
                                                                  nelem_zeta      = 1)
            


            else




                group_names(1,:) = [string_t("Left Boundary")  , &
                                    string_t("Right Boundary") , &
                                    string_t("YZ Boundaries")  , &
                                    string_t("YZ Boundaries")  , &
                                    string_t("YZ Boundaries")  , &
                                    string_t("YZ Boundaries") ]

                call create_mesh_file('D1 NxNxN',this%gridfile,equation_sets   = [string_t("Test 1D Linear Advection Diffusion")],  &
                                                               group_names     = group_names,                                       &
                                                               bc_state_groups = bc_state_groups,                                   &
                                                               nelem_xi        = this%nelem_xi,                                     &
                                                               nelem_eta       = 1,                                                 &
                                                               nelem_zeta      = 1)
            end if !overset 

        end if !GLOBAL_MASTER


        ! Set ChiDG components
        call chidg%set('Solution Order',   integer_input=this%order)
        call chidg%set('Time Integrator',  algorithm=time_integrator)
        call chidg%set('Nonlinear Solver', algorithm=nonlinear_solver, options=noptions)
        call chidg%set('Linear Solver',    algorithm=linear_solver,    options=loptions)
        call chidg%set('Preconditioner',   algorithm=preconditioner)

        ! Read grid
        call chidg%read_mesh(this%gridfile)

        ! Initialize solution
        call create_function(fcn,'constant')
        call fcn%set_option('val',0.1_rk)
        call chidg%data%sdata%q_in%project(chidg%data%mesh,fcn,1)

    end subroutine SetUp
    !*****************************************************************************************



    !>
    !!
    !!
    !-----------------------------------------------------------------------------------------
    subroutine tearDown(this)
        class(linear_advection_diffusion_1D),  intent(inout)   :: this

        ! Close ChiDG
        call chidg%shut_down('core')
        call chidg%shut_down('mpi')

        if (IRANK == GLOBAL_MASTER) then
            call delete_file(this%gridfile)
        end if

    end subroutine tearDown
    !*****************************************************************************************






    !>  Run the simulation.
    !!
    !!      - compute the entropy error, test against the reference values.
    !!      - store the nonlinear convergence to the module variable so it can 
    !!        be used by a test for nonlinear convergence.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !!
    !-----------------------------------------------------------------------------------------
    @Test(npes=[1,2,3])
    subroutine run_calculations(self)
        class(linear_advection_diffusion_1D),  intent(inout)   :: self

        real(rk)    :: computed_error
        integer(ik) :: npes_index, grid_index, order_index, backend_index

        npes_index  = get_npes_index(NRANK)
        grid_index  = get_grid_index(self%nelem_xi)
        order_index = get_order_index(self%order)
        backend_index = get_backend_index(self%backend)


        ! Run ChiDG simulation
        call chidg%run(write_initial=.false., write_final=.false.)


        ! Store the number of nonlinear iterations that the solution converged in. 
        ! Problem is linear so it should converge in 1 Newton Step. Really, just the 
        ! solution of 1 linear system of equations.
        if (self%gridtype == 'single') then
            nsteps_convergence(npes_index,grid_index,order_index,backend_index) = chidg%nonlinear_solver%newton_iterations
        elseif (self%gridtype == 'overset') then
            nsteps_convergence_overset(npes_index,grid_index,order_index,backend_index) = chidg%nonlinear_solver%newton_iterations
        end if


        ! Compute error
        computed_error = compute_1D_linear_advection_diffusion_error(chidg%data,chidg_COMM)


        ! Store the computed error for the current case
        if (self%gridtype == 'single') then
            ! conforming grids
            error(npes_index,grid_index,order_index,backend_index)         = computed_error
        elseif (self%gridtype == 'overset') then
            ! overset grids
            error_overset(npes_index,grid_index,order_index,backend_index) = computed_error
        end if


    end subroutine run_calculations
    !*****************************************************************************************








    !>  Access the stored number of steps convergence data(module global variable) and confirm
    !!  that all simulations converged in 1 step.
    !!
    !!  This assumes that the 'run_calculations' routine has already been executed for all cases
    !!  and numbers of processors to that the 'nsteps_convergence' variable has been populated.
    !!  So, we are relying on pFUnit to do things in order here. Works now, but I don't think
    !!  there is a guaruntee that this can't change.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !-----------------------------------------------------------------------------------------
    @Test(npes=[1])
    subroutine test_conv_conform(self)
        class(linear_advection_diffusion_1D),  intent(inout)   :: self

        integer(ik) :: inpes, igrid, iorder, nsteps, ibackend

        do inpes = 1,3
            do igrid = 1,2
                do iorder = 1,3
                    do ibackend = 1,2

                        ! Test that the current convergence for the processor count matches 
                        ! the serial convergence.
                        nsteps = nsteps_convergence(inpes,igrid,iorder,ibackend)%size()
                        @assertEqual(1, nsteps)

                    end do
                end do
            end do
        end do


    end subroutine test_conv_conform
    !*****************************************************************************************





    !>  Access the stored number of steps convergence data(module global variable) and confirm
    !!  that all simulations converged in 1 step.
    !!
    !!  This assumes that the 'run_calculations' routine has already been executed for all cases
    !!  and numbers of processors to that the 'nsteps_convergence' variable has been populated.
    !!  So, we are relying on pFUnit to do things in order here. Works now, but I don't think
    !!  there is a guaruntee that this can't change.
    !!
    !!  @author Nathan A. Wukie
    !!  @author Nathan A. Wukie (AFRL)
    !!
    !-----------------------------------------------------------------------------------------
    @Test(npes=[1])
    subroutine test_conv_overset(self)
        class(linear_advection_diffusion_1D),  intent(inout)   :: self

        integer(ik) :: inpes, igrid, iorder, nsteps, ibackend

        do inpes = 1,3
            do igrid = 1,2
                do iorder = 1,3
                    do ibackend = 1,2

                        ! Test that the current convergence for the processor count matches the serial convergence
                        nsteps = nsteps_convergence_overset(inpes,igrid,iorder,ibackend)%size()
                        @assertEqual(1, nsteps)

                    end do
                end do
            end do
        end do

    end subroutine test_conv_overset
    !*****************************************************************************************




    !>
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   9/16/2016
    !!
    !----------------------------------------------------------------------------------------
    @Test(npes=[1])
    subroutine test_err_conforming(self)
        class(linear_advection_diffusion_1D),  intent(inout)   :: self

        integer(ik) :: inpes, igrid, iorder, ibackend
        real(rk)    :: error_previous, error_current, mesh_parameter_previous, mesh_parameter_current, convergence_rate, tol

        tol = 0.1_rk


        do inpes = 1,3
            do igrid = 2,2  ! Start at the second grid because we need the previous grid in order to compute a rate 
                do iorder = 1,3
                    do ibackend = 1,2

                        error_previous = error(inpes,igrid-1,iorder,ibackend)
                        error_current  = error(inpes,igrid  ,iorder,ibackend)

                        mesh_parameter_previous = mesh_parameter(igrid-1,iorder)
                        mesh_parameter_current  = mesh_parameter(igrid  ,iorder)

                        ! Compute convergence rate of spatial operators. log10(error)/log10(mesh spacing)
                        convergence_rate = (log10(error_previous)-log10(error_current))/(log10(mesh_parameter_previous)-log10(mesh_parameter_current))
                    
                        @assertGreaterThan(convergence_rate, real(iorder+1,rk)-tol)
                        @assertLessThan(convergence_rate, real(iorder+1,rk)+tol)

                    end do
                end do
            end do
        end do


    end subroutine test_err_conforming
    !*****************************************************************************************




    !>
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   9/16/2016
    !!
    !-----------------------------------------------------------------------------------------
    @Test(npes=[1])
    subroutine test_err_overset(self)
        class(linear_advection_diffusion_1D),  intent(inout)   :: self

        integer(ik) :: inpes, igrid, iorder, ibackend
        real(rk)    :: error_previous, error_current, mesh_parameter_previous, mesh_parameter_current, convergence_rate, tol

        tol = 0.1_rk

        do inpes = 1,3
            do igrid = 2,2  ! Start at the second grid because we need the previous grid in order to compute a rate 
                do iorder = 1,3
                    do ibackend = 1,2

                        error_previous = error_overset(inpes,igrid-1,iorder,ibackend)
                        error_current  = error_overset(inpes,igrid  ,iorder,ibackend)

                        mesh_parameter_previous = mesh_parameter(igrid-1,iorder)
                        mesh_parameter_current  = mesh_parameter(igrid  ,iorder)

                        ! Compute convergence rate of spatial operators. log10(error)/log10(mesh spacing)
                        convergence_rate = (log10(error_previous)-log10(error_current))/(log10(mesh_parameter_previous)-log10(mesh_parameter_current))
                    
                        @assertGreaterThan(convergence_rate, real(iorder+1,rk)-tol)
                        @assertLessThan(convergence_rate, real(iorder+1,rk)+tol)

                    end do
                end do
            end do
        end do


    end subroutine test_err_overset
    !*****************************************************************************************






    !>  Function to compute the error in the 1D scalar diffusion equation.
    !!
    !!  The error is computed as the L2-Norm of the difference in the analytical and
    !!  numerical solutions integrated over the domain.
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   9/16/2016
    !!
    !!
    !----------------------------------------------------------------------------------------
    function compute_1D_linear_advection_diffusion_error(data,ChiDG_COMM) result(error)
        type(chidg_data_t), intent(inout)   :: data
        type(mpi_comm),     intent(in)      :: ChiDG_COMM

        real(rk), allocatable, dimension(:) :: &
            u_dg, u_analytical, x_gq, gq_error_squared


        integer(ik) :: ielem, idom, ierr, itime, iu
        real(rk)    :: error_squared_sum, error_squared_sum_reduced, element_error_squared, error


        associate (mesh => data%mesh, sdata => data%sdata, eqnset => data%eqnset, prop => data%eqnset(1)%prop)

            ! Get equation indices
            iu = prop%get_primary_field_index("u")

            ! Zero error accumulator
            error_squared_sum = ZERO

            ! Loop over elements and accumulate error
            itime = 1
            do idom = 1,data%mesh%ndomains()
                do ielem = 1,data%mesh%domain(idom)%nelem

                    ! Compute the analytical solution at the quadrature nodes
                    x_gq = mesh%domain(idom)%elems(ielem)%interp_coords(:,1)
                    u_analytical = (ONE - exp((x_gq-ONE)*(cx/mu))) / (ONE - exp(-cx/mu))

                    ! Interpolate DG numerical solution
                    u_dg = interpolate_element_standard(mesh,sdata%q,idom,ielem,iu,itime,'value')

                    ! Compute error at GQ nodes
                    gq_error_squared = (u_analytical - u_dg)**TWO

                    ! Integrate error
                    element_error_squared = sum(gq_error_squared * mesh%domain(idom)%elems(ielem)%jinv * mesh%domain(idom)%elems(ielem)%basis_s%weights_element())

                    ! Accumulate error from each element
                    error_squared_sum = error_squared_sum + element_error_squared

                end do ! ielem
            end do ! idom

        end associate

        ! Reduce the total error across processors
        call MPI_AllReduce(error_squared_sum,error_squared_sum_reduced,1,MPI_REAL8,MPI_SUM,ChiDG_COMM,ierr)

        ! Sqrt of the reduced error
        error = sqrt(error_squared_sum_reduced)

    end function compute_1D_linear_advection_diffusion_error
    !*****************************************************************************************



    !>
    !!
    !!  @author Nathan A. Wukie (AFRL)
    !!  @date   9/16/2016
    !!
    !----------------------------------------------------------------------------------------
    function mesh_parameter(igrid,iorder) result(param)
        integer(ik),    intent(in)  :: igrid
        integer(ik),    intent(in)  :: iorder

        integer(ik) :: nelem, nterms_1d
        real(rk)    :: param

        select case (igrid)
            case(1)
                nelem = 60
            case(2)
                nelem = 120
            case default
                call chidg_signal(FATAL,"integration_1D_linear_diffusion_mod%mesh_parameter: invalid igrid")
        end select


        select case (iorder)
            case(1) ! iorder = 1  =>  order = 2
                nterms_1d = (iorder+1)
            case(2)
                nterms_1d = (iorder+1)
            case(3)
                nterms_1d = (iorder+1)
            case default
                call chidg_signal(FATAL,"integration_1D_linear_diffusion_mod%mesh_parameter: invalid iorder")
        end select


        param = ONE/(real(nelem,rk)*real(nterms_1d,rk))

    end function mesh_parameter
    !****************************************************************************************



    ! Utility for returning the index associated with a certain number of procs
    !----------------------------------------------------------------------------------------
    function get_npes_index(numproc) result(inpes)
        integer(ik), intent(in) :: numproc

        integer(ik) :: inpes

        select case (numproc)
            case(1)
                inpes = 1
            case(2)
                inpes = 2
            case(3)
                inpes = 3
        end select

    end function get_npes_index
    !****************************************************************************************



    ! Utility for returning the index associated with a certain grid file
    !----------------------------------------------------------------------------------------
    function get_grid_index(nelem_xi) result(igrid)
        integer(ik),    intent(in)  :: nelem_xi

        integer(ik) :: igrid

        select case (nelem_xi)
            case(60)
                igrid = 1
            case(120)
                igrid = 2
        end select

    end function get_grid_index
    !****************************************************************************************



    ! Utility for returning the index associated with a certain order of accuracy
    !----------------------------------------------------------------------------------------
    function get_order_index(order) result(iorder)
        integer(ik), intent(in) :: order

        integer(ik) :: iorder

        select case (order)
            case(2)
                iorder = 1
            case(3)
                iorder = 2
            case(4)
                iorder = 3
        end select

    end function get_order_index
    !****************************************************************************************


    ! Utility for returning the index associated with a certain number of procs
    !------------------------------------------------------------------------------
    function get_backend_index(backend) result(ibackend)
        character(*),   intent(in)  :: backend

        integer(ik) :: ibackend

        select case (trim(backend))
            case('native')
                ibackend = 1
            case('petsc')
                ibackend = 2
            case default
                call chidg_signal_one(FATAL,'Invalid backend.', trim(backend))
        end select

    end function get_backend_index
    !******************************************************************************





end module integration_1D_linear_adv_diff_mod
